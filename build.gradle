plugins {
    id 'java-library'
    id 'maven-publish'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'org.asciidoctor.jvm.pdf' version '3.3.2'
}


version '8.0.1'+System.getProperty('release','-SNAPSHOT')
group 'ch.interlis'


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 8
    options.encoding = 'US-ASCII'
}

// In this section you declare where to find the dependencies of your project
repositories {
	mavenLocal()
    mavenCentral()
    maven {
        url "https://jars.interlis.ch"
    }
}

Properties properties = new Properties()
File propFile=project.rootProject.file('user.properties')
if(propFile.exists()){
	properties.load(propFile.newDataInputStream())
}
def git = System.getProperty('git',properties.get('git','git'))
def repos_pwd = System.getProperty('repos_pwd',properties.get('repos_pwd','repos_pwd'))
def repos_usr = System.getProperty('repos_usr',properties.get('repos_usr','repos_usr'))
def repos_url = System.getProperty('repos_url',properties.get('repos_url','repos_url'))
def downloads_pwd = System.getProperty('downloads_pwd',properties.get('downloads_pwd','downloads_pwd'))
def downloads_usr = System.getProperty('downloads_usr',properties.get('downloads_usr','downloads_usr'))


def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine git, 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def generatedResources = "$buildDir/generated-resources/main"
def extractedIli2cJar = "$buildDir/ili2cJar"
def extractedJtsJar = "$buildDir/jtsJar"

sourceSets {
    main {
    	output.dir(generatedResources, builtBy: 'generateMyResources')
        java {
            srcDirs = ['src']
        }
        //compileClasspath += project.configurations.compileOnly
    }
}

task generateMyResources {
	def versionPropsFile = new File(generatedResources,"ch/interlis/ili2fme/Version.properties")
	def version="$project.version"
	def hash=getGitHash()
	inputs.property("version","$project.version")
	inputs.property("hash",getGitHash())
    outputs.files "$versionPropsFile"
	doLast {
		def versionProps = new Properties()
		versionProps.setProperty('version', version)
		versionProps.setProperty('versionCommit', hash)
		versionPropsFile.getParentFile().mkdirs();
		versionProps.store(versionPropsFile.newWriter(), null);
	}
}


configurations {
    ftpAntTask
    ili2cTool { transitive = false }
    jtsJar { transitive = false }
}


dependencies {
    implementation files("lib/pluginbuilder.jar")
    implementation "com.vividsolutions:jts-core:1.14.0"
	implementation "ch.interlis:ili2c-tool:5.6.6"
	implementation "ch.interlis:ili2c-core:5.6.6"
    implementation "ch.interlis:iox-ili:1.24.1"
    jtsJar "com.vividsolutions:jts-core:1.14.0"
   ili2cTool("ch.interlis:ili2c-tool:5.6.6"){
		artifact {
			  //useful when some artifact properties unconventional
			  name = 'ili2c-tool' //artifact name different than module name
			  extension = 'zip'
			  classifier = 'bindist'
		}
   }
    ftpAntTask "org.apache.ant:ant-commons-net:1.10.15" // muss zu gradle builtin ant version passen!
}

jar {
  enabled = true
  manifest {
    attributes(
      "Main-Class": 'ch.interlis.ili2fme.AboutDialog',
      "Class-Path": 'ili2c.jar jts-core-1.14.0.jar'
    )
  }
}

asciidoctor {
    sourceDir  file('docs')
    outputDir  file('build/asciidoctor')
	resources {
	  from('docs') {
		include 'media/*.png'
	  }
	}
}

asciidoctorPdf {
    sourceDir  file('docs')
	sources {
		include 'ili2fme.adoc' //, 'another.adoc', 'third.adoc'
	} 
	//options '-o': 'test.pdf'
	//attributes '-o' : 'test.pdf'
	
  	outputDir  file('build/asciidoctor')
  	baseDirFollowsSourceDir()
	/*attributes {
	  imagesdir= 'docs'
	  //'imagesoutdir' => './images'
	} */ 	
}

task getIli2cToolJar(type: Copy){
	from {
		zipTree(configurations.ili2cTool.singleFile).matching { include '**/ili2c.jar' }.singleFile
	}
	into "$extractedIli2cJar"
}
task getJtsJar(type: Copy){
	from {
		configurations.jtsJar.singleFile
	}
	into "$extractedJtsJar"
}


task bindist(type: Zip,dependsOn: [asciidoctorPdf]){
	group = BasePlugin.BUILD_GROUP
    description = 'Assembles the binary distribution of this project'
	
	archiveBaseName = project.name
	destinationDirectory = file('dist')
	
	def fme="FME Suite"
	
	from(jar) {
		rename '.+','ili2fme.jar'
        into "${fme}/plugins/"
	}
	from(getIli2cToolJar){
        into "${fme}/plugins/"
	}
	from(getJtsJar){
        into "${fme}/plugins/"
	}
	from("other"){
		include "ch.ehi.fme.Main.fmf"
        into "${fme}/metafile/"
	}
	from("other"){
		include "interlis2.db"
        into "${fme}/formatsinfo/"
	}
	from("other"){
	    include "ilimodels/*.ili", "ili22models/*.ili","converter/*.fmi","iligml/**/*.xsd"
        into "${fme}/plugins/interlis2/"
	}
	from ("$projectDir"){
	    include "docs/LICENSE.*","docs/README.md","docs/CHANGELOG.txt"
	}
	from ("$buildDir/asciidoctor"){
	    include "ili2fme.pdf"
        into "docs/"
	}
}


task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    from ('.'){
    	include 'docs/*.adoc'
    	include 'docs/media/*.png'
    }
}

javadoc{
	failOnError = false
	options.addBooleanOption 'Xdoclint:none', true
	//options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

def githubRepoUrl = 'https://github.com/claeis/ili2fme'

def pomConfig = {
    licenses {
        license {
            name = "LGPL 2.1"
            url = "https://github.com/claeis/ili2fme/blob/master/docs/LICENSE.lgpl"
        }
    }
    developers {
        developer {
            id = "claeis"
            name = "Claude Eisenhut"
            email = "ce@eisenhutinformatik.ch"
        }
    }

    scm {
        url = githubRepoUrl
    }
}

publishing {
	publications {
        maven(MavenPublication) {
            artifact jar
            pom pomConfig
        }
        maven(MavenPublication) {
            artifact sourcesJar
        }
        maven(MavenPublication) {
            artifact javadocJar
        }
        maven(MavenPublication) {
            artifact(bindist){
            	extension 'zip'
            	classifier 'bindist'
            }
        }
    }
    repositories {
        maven {
            url = repos_url
            credentials {
            	username = repos_usr
            	password = repos_pwd
            }            
        }
    }
}

task uploadBin(dependsOn: [bindist,asciidoctorPdf]) {
	doLast {
		new File("$buildDir/asciidoctor/ili2fme.pdf").renameTo("$buildDir/asciidoctor/ili2fme-${project.version}.pdf")

		ant.taskdef(name: 'ftp',
					classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
					classpath: configurations.ftpAntTask.asPath)
		def dist= bindist.archiveFile.get().asFile.parent
		def name=bindist.archiveFile.get().asFile.name
		def json = groovy.json.JsonOutput.toJson([filename: 'https://downloads.interlis.ch/ili2fme/'+name, version: project.version ,date: new Date().format( 'yyyy-MM-dd' )])
		def releaseFile = new File(dist,project.name+"-release.json")
		releaseFile.write(json)
		ant.ftp(server: "jql.ftp.infomaniak.com", userid: downloads_usr, password: downloads_pwd, 
			remoteDir: "/ili2fme", passive:"yes") {
				fileset(dir: dist ) {
					include(name: name)
					include(name: releaseFile.name)
				}
				fileset(dir: "$buildDir/asciidoctor/" ) {
					include(name: "ili2fme-${project.version}.pdf")
				}
		}
	}
}